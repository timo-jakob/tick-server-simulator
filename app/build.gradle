plugins {
    id 'application'
    id "org.sonarqube" version "6.0.1.5171"
    id 'jacoco'
    id 'com.google.protobuf' version '0.9.4'
    id 'jvm-test-suite' // Apply the jvm-test-suite plugin
}

repositories {
    // Use Maven Central for resolving dependencies.
    mavenCentral()
}

ext {
    grpcVersion = '1.72.0'
    protoVersion = '4.29.3'
}

dependencies {
    // This dependency is used by the application.
    implementation 'com.google.guava:guava:33.4.0-jre'

    // grpc (see https://github.com/grpc/grpc-java)
    implementation "io.grpc:grpc-netty-shaded:${grpcVersion}"
    implementation "io.grpc:grpc-protobuf:${grpcVersion}"
    implementation "io.grpc:grpc-stub:${grpcVersion}"
    compileOnly "org.apache.tomcat:annotations-api:6.0.53" // necessary for Java 9+


    implementation "com.google.protobuf:protobuf-java:${protoVersion}"
    implementation "com.google.protobuf:protobuf-java-util:${protoVersion}"
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:${protoVersion}"
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}

sourceSets {
    main {
        java {
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/java'
        }
    }
}

application {
    // Define the main class for the application.
    mainClass.set("tickserver.TickServerSimulator")
}

testing {
    suites {
        test {
            useJUnitJupiter() // Use JUnit Jupiter for testing
        }
    }
}

sonarqube {
    properties {
        property "sonar.projectKey", "timo-jakob_tick-server-simulator"
        property "sonar.organization", "timo-jakob-github"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}

jacocoTestReport {
    reports {
        xml.required = true
    }
}

jacoco {
    toolVersion = "0.8.12"
}

project.tasks["sonarqube"].dependsOn "jacocoTestReport"