plugins {
    id 'application'
    id "org.sonarqube" version "4.3.0.3225"
    id 'jacoco'
    id 'com.google.protobuf' version '0.9.4'
}

repositories {
    // Use Maven Central for resolving dependencies.
    mavenCentral()
}

def grpcVersion = "1.57.1"
def protoVersion = "3.23.4"

dependencies {
    // Use JUnit Jupiter API for testing.
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.0'

    // Use JUnit Jupiter Engine for testing.
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'

    // This dependency is used by the application.
    implementation 'com.google.guava:guava:32.1.1-jre'

    // grpc (see https://github.com/grpc/grpc-java)
    implementation "io.grpc:grpc-netty-shaded:${grpcVersion}"
    implementation "io.grpc:grpc-protobuf:${grpcVersion}"
    implementation "io.grpc:grpc-stub:${grpcVersion}"
    compileOnly "org.apache.tomcat:annotations-api:6.0.53" // necessary for Java 9+
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:${protoVersion}"
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}

sourceSets {
    main {
        java {
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/java'
        }
    }
}

application {
    // Define the main class for the application.
    mainClass.set("tickserver.TickServerSimulator")
}

tasks.named('test') {
    // Use junit platform for unit tests.
    useJUnitPlatform()
}

sonarqube {
    properties {
        property "sonar.projectKey", "timo-jakob_tick-server-simulator"
        property "sonar.organization", "timo-jakob-github"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}

jacocoTestReport {
    reports {
        xml.required = true
    }
}

jacoco {
    toolVersion = "0.8.10"
}

project.tasks["sonarqube"].dependsOn "jacocoTestReport"
